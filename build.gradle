buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath(
                "com.adarshr:gradle-test-logger-plugin:1.5.0",
                "com.github.jengelman.gradle.plugins:shadow:4.0.3",
                "com.gradle:build-scan-plugin:2.3",
                "org.owasp:dependency-check-gradle:4.0.2",
                "org.springframework.boot:spring-boot-gradle-plugin:2.1.1.RELEASE",
        )
    }
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: "com.gradle.build-scan"
apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'com.adarshr.test-logger'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
//    implementation('org.springframework.boot:spring-boot-starter-web') {
//        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
//    }
    implementation(
            'org.springframework.boot:spring-boot-starter-web',
            'com.amazonaws.serverless:aws-serverless-java-container-springboot2:[1.0,)',
            'io.symphonia:lambda-logging:1.0.1'
    )
    testCompile("junit:junit")
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from(configurations.compileClasspath) {
            exclude 'tomcat-embed-*'
        }
    }
}

shadowJar {
    classifier = 'aws'
    dependencies {
        exclude('tomcat-embed-*')
    }
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories']
        mergeStrategy = "append"
    }
}

build.dependsOn buildZip
build.dependsOn shadowJar
